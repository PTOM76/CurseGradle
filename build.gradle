plugins {
    id 'groovy'
    id 'idea'
    id 'java-gradle-plugin'
    id 'com.gradle.plugin-publish' version '1.3.0'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

ext {
    inceptionYear = '2015'
    url = 'https://github.com/PTOM76/CurseGradle'
}

version = '1.5.0'
group = 'com.matthewprenger'
description = 'Gradle plugin to upload artifacts to CurseForge'
base.archivesName = 'CurseGradle'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compileOnly gradleApi()
    compileOnly localGroovy()

    implementation 'net.sf.trove4j:trove4j:3.0.3'
    implementation 'com.google.guava:guava:33.0.0-jre'
    implementation 'com.google.code.gson:gson:2.11.0'
    implementation('org.apache.httpcomponents:httpmime:4.5.14') {
        exclude group: 'commons-codec', module: 'commons-codec'
    }
    implementation('org.apache.httpcomponents:httpclient:4.5.14') {
        exclude group: 'commons-codec', module: 'commons-codec'
    }
    implementation 'commons-codec:commons-codec:1.17.1'
}

configurations.configureEach {
    resolutionStrategy {
        force 'commons-codec:commons-codec:1.17.1'
    }
}

jar.manifest.mainAttributes([
        'Implementation-Title'  : project.name,
        'Implementation-Version': project.version
])

shadowJar {
    archiveClassifier.set('')
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

tasks.named('processResources') {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

def javadocJar = tasks.register('javadocJar', Jar) {
    dependsOn javadoc
    from javadoc.destinationDir
    archiveClassifier.set('javadoc')
}

def sourcesJar = tasks.register('sourcesJar', Jar) {
    from sourceSets.main.allSource
    archiveClassifier.set('sources')
}


assemble.dependsOn javadocJar, sourcesJar

[compileJava, compileGroovy].each {
    it.options.encoding = 'UTF-8'
    it.options.deprecation = true
    it.options.fork = true
}

gradlePlugin {
    website = project.url
    vcsUrl = 'https://github.com/PTOM76/CurseGradle'
    plugins {
        curseGradlePlugin {
            id = 'com.matthewprenger.cursegradle'
            implementationClass = 'com.matthewprenger.cursegradle.CurseGradlePlugin'
            description = project.description
            displayName = 'CurseGradle'
            tags = ['curseforge', 'publish', 'minecraft']
        }
    }
}
