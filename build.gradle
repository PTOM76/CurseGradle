plugins {
    id 'groovy'
    id 'idea'
    id 'java-gradle-plugin'
    id 'com.gradle.plugin-publish' version '1.3.0'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

ext {
    inceptionYear = '2015'
    url = 'https://github.com/PTOM76/CurseGradle'
}

version = '1.5.0'
group = 'com.matthewprenger'
description = 'Gradle plugin to upload artifacts to CurseForge'
base.archivesName = 'CurseGradle'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compileOnly gradleApi()
    compileOnly localGroovy()

    implementation 'net.sf.trove4j:trove4j:3.0.3'
    implementation 'com.google.guava:guava:33.0.0-jre'
    implementation 'com.google.code.gson:gson:2.11.0'
    implementation 'org.apache.httpcomponents.client5:httpclient5:5.4.3'
}

jar.manifest.mainAttributes([
        'Implementation-Title'  : project.name,
        'Implementation-Version': project.version
])

shadowJar {
    archiveClassifier.set('')
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

tasks.named('processResources') {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

def javadocJar = tasks.register('javadocJar', Jar) {
    dependsOn javadoc
    from tasks.named('javadoc').get().destinationDir
    archiveClassifier.set('javadoc')
}

def sourcesJar = tasks.register('sourcesJar', Jar) {
    from sourceSets.main.allSource
    archiveClassifier.set('sources')
}


assemble.dependsOn javadocJar, sourcesJar

[compileJava, compileGroovy].each {
    it.options.encoding = 'UTF-8'
    it.options.deprecation = true
    it.options.fork = true
}

gradlePlugin {
    website = project.url
    vcsUrl = 'https://github.com/PTOM76/CurseGradle'
    plugins {
        curseGradlePlugin {
            id = 'com.matthewprenger.cursegradle'
            implementationClass = 'com.matthewprenger.cursegradle.CurseGradlePlugin'
            description = project.description
            displayName = 'CurseGradle'
            tags = ['curseforge', 'publish', 'minecraft']
        }
    }
}

// configure the maven publication
publishing {
    publications {
        create('mavenJava', MavenPublication) {
            // add all the jars that should be included when publishing to maven
            artifactId = 'cursegradle'
            artifact(shadowJar) {
                builtBy shadowJar
            }
            artifact(javadocJar) {
                builtBy javadocJar
            }
            artifact(sourcesJar) {
                builtBy sourcesJar
            }
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/PTOM76/maven")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_ACTOR")
                password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
            }
        }
    }
}

afterEvaluate {
    publishing.publications.named('pluginMaven', MavenPublication) {
        // Override artifactId to lowercase to match mavenJava and avoid 422 conflicts
        artifactId = 'cursegradle'
    }

    // Also publish plugin marker to GitHub Packages
    publishing.publications.named('curseGradlePluginPluginMarkerMaven', MavenPublication) {
        // This is the auto-generated marker publication - keep default naming
    }
}

// Compatibility patch for Shadow on Gradle 9.x (adds missing 'mode' property stub)
try {
    Class.forName('org.gradle.api.internal.file.copy.NormalizingCopyActionDecorator$StubbedFileCopyDetails')
            .metaClass.getMode = { -> 0 }
} catch (Throwable ignored) {
    // No-op for Gradle versions where the class does not exist
}

try {
    Class.forName('org.gradle.api.internal.file.copy.DefaultFileCopyDetails_Decorated')
            .metaClass.getMode = { -> 0 }
} catch (Throwable ignored) {
    // No-op for Gradle versions where the class does not exist
}

try {
    Class.forName('org.gradle.api.internal.file.copy.DefaultFileCopyDetails')
            .metaClass.getMode = { -> 0 }
} catch (Throwable ignored) {
    // No-op for Gradle versions where the class does not exist
}
